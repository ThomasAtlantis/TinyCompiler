
$程序 -> $全局变量声明 $其他函数定义 $全局变量声明 $主函数定义
$全局变量声明 -> $声明语句1 ; | null
$主函数定义 -> void main ( ) { $函数体 }
$其他函数定义 -> $函数定义 $其他函数定义 | null
$函数定义 -> void $函数标识符 ( $形参列表 ) { $函数体 }
$形参列表 -> $形参 $其他形参
$其他形参 -> , $形参 | null
$形参 -> $类型 $变量标识符 | $引用类型 $变量标识符
$引用类型 -> $类型 &
$函数体 -> $总结构 $函数体 | null
$总结构 -> $顺序结构 | $循环结构 | $分支结构1
$顺序结构 -> $语句 ; $顺序结构1
$顺序结构1 -> $语句 ; $顺序结构1 | null
$分支结构1 -> if ( $表达式 ) { $函数体 } $分支结构2
$分支结构2 -> else if ( $表达式 ) { $函数体 } $分支结构2 | else { $函数体 } | null
$循环结构 -> while ( $表达式 ) { $函数体 $循环跳转 $函数体 }
$循环结构 -> for ( $for_list_0 ; $for_list_1 ; $for_list_2 ) { $函数体 $循环跳转 $函数体 }
$for_list_0 -> $语句 | null
$for_list_1 -> $表达式 | null
$for_list_2 -> $语句 | null
$循环跳转 -> continue ; | break ;
$语句 -> $声明语句1 | $赋值语句 | return | $输出语句
$输出语句 -> put $字符型变量 | put $字符型常量 | put $字符串常量
$声明初始化 -> = $表达式 | null
$赋值语句 -> $左值 = $表达式
$跳转语句 -> return
$类型 -> int | float | bool | char
$常量 -> @C
$左值 -> $变量标识符

$表达式 -> $逻辑运算 | $tmp8
	$逻辑运算 -> $tmp1_2 $tmp1_1
	$tmp1_1 -> || $tmp1_2 $tmp1_1 | null
	$tmp1_2 -> $tmp1_4 $tmp1_3
	$tmp1_3 -> && $tmp1_4 $tmp1_3 | null
	$tmp1_4 -> $tmp2
$tmp2 -> $关系运算 | $tmp8
	$关系运算 -> $tmp3_2 $tmp3_1
	$tmp3_1 -> > $tmp3_2 | >= $tmp3_2 | < $tmp3_2 | <= $tmp3_2 | == $tmp3_2 | != $tmp3_2 | null
	$tmp3_2 -> $tmp4
$tmp4 -> $算术运算 | $tmp8
	$算术运算 -> $tmp5_2 $tmp5_1
	$tmp5_1 -> + $tmp5_2 $tmp5_1 | - $tmp5_2 $tmp5_1 | null
	$tmp5_2 -> $tmp5_4 $tmp5_3
	$tmp5_3 -> * $tmp5_4 $tmp5_3 | / $tmp5_4 $tmp5_3 | null
	$tmp5_4 -> $tmp6
$tmp6 -> $位运算 | $tmp8
	$位运算 -> $tmp7_2 $tmp7_1
	$tmp7_1 -> '| $tmp7_2 $tmp7_1 | null
	$tmp7_2 -> $tmp7_4 $tmp7_3
	$tmp7_3 -> & $tmp7_4 $tmp7_3 | null
	$tmp7_4 -> $tmp8
$tmp8 -> $原子结合 | ! $原子结合 | - $原子结合 | ~ $原子结合
$原子结合 -> $变量标识符 | $常量 | ( $表达式 )

$语句 -> $类型自定义
$声明语句1 -> $类型 $可初始化标识符 $声明初始化
$声明语句1 -> $类型 $数组变量 [ $数组索引 ]
$声明语句1 -> struct $自定义结构类型 $自定义结构变量

$声明语句2 -> $类型 $可初始化标识符
$声明语句2 -> $类型 $数组变量 [ $数组索引 ]
$声明语句2 -> struct $自定义结构类型 $自定义结构变量

$可初始化标识符 -> $整型变量 | $实型变量 | $布尔变量
$类型自定义 -> struct $自定义结构类型 { $声明语句2 ; $声明语句块 }
$声明语句块 -> $声明语句2 ; $声明语句块 | null
$标识符 -> $函数标识符 | $变量标识符 | $自定义结构类型
$数组索引 -> $整型变量 | $整型常量
$变量标识符 -> $整型变量 | $实型变量 | $布尔变量 | $自定义结构变量 | $数组变量 [ $数组索引 ]
$函数标识符 -> @FUNC
$自定义结构类型 -> @T
$自定义结构变量 -> @S
$字符型变量 -> @CH
$整型变量 -> @I
$实型变量 -> @F
$布尔变量 -> @B
$数组变量 -> @A
$常量 -> $整型常量 | $实型常量 | $布尔常量
$整型常量 -> @C_I
$实型常量 -> @C_F
$字符型常量 -> @C_CH
$字符串常量 -> @C_S
$布尔常量 -> true | false
